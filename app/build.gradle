def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    applicationVariants.all { variant -> // .all stejně jako each prochází všechny prvky kolekce, ale aplikuje se i v případě, že v ní přibudou nové prvky
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk")
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "uco_422217.movio2.pv256.fi.muni.cz.filmsexcdomkar"
        minSdkVersion 15
        targetSdkVersion 24

        resConfigs "cs", "ja", "en"

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    compileOptions { //chceme psát syntaxí javy 7
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        orig {
            buildConfigField "boolean", "logging", "true"
            applicationId "uco_422217.movio2.pv256.fi.muni.cz.filmsexcdomkar.orig"
        }
        second {
            buildConfigField "boolean", "logging", "false"
            applicationId "uco_422217.movio2.pv256.fi.muni.cz.filmsexcdomkar.second"
            //úplně jiný package name
            versionCode 10
            versionName "10"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:palette-v7:24.2.1'
    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'net.danlew:android.joda:2.7.2'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.4.2'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.4.2'
    compile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
}
